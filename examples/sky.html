<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head>
<meta http-equiv="Content-Type" content="text/html; charset=iso-8859-1" />

<title>rendeer.js: sky example</title>
	<link rel="stylesheet" type="text/css" href="style.css" />
	<style type='text/css'>
		html, body { width: 100%; height: 100%; margin: 0; padding: 0 }
	</style>
	<script type="text/javascript" src="../external/gl-matrix-min.js"></script>
	<script type="text/javascript" src="../external/litegl.js"></script>
	<script type="text/javascript" src="../build/rendeer.js"></script>
	<script type="text/javascript">




var IntegrateScattering = `
//From https://github.com/Fewes/MinimalAtmosphere

precision highp float;

#define EPS                 1e-6
#define PI                  3.14159265359
#define INFINITY            1.0 / 0.0
#define PLANET_RADIUS       6371000.0
#define PLANET_CENTER       vec3(0.0, -PLANET_RADIUS, 0.0)
#define ATMOSPHERE_HEIGHT   100000.0
#define RAYLEIGH_HEIGHT     (ATMOSPHERE_HEIGHT * 0.08)
#define MIE_HEIGHT          (ATMOSPHERE_HEIGHT * 0.012)

// -------------------------------------
// Coefficients
#define C_RAYLEIGH          (vec3(5.802, 13.558, 33.100) * 1e-6)
#define C_MIE               (vec3(3.996,  3.996,  3.996) * 1e-6)
#define C_OZONE             (vec3(0.650,  1.881,  0.085) * 1e-6)

#define ATMOSPHERE_DENSITY  1.0
#define EXPOSURE            20.0

// -------------------------------------
// Math
vec2 SphereIntersection(vec3 rayStart, vec3 rayDir, vec3 sphereCenter, float sphereRadius)
{
	rayStart -= sphereCenter;
	float a = dot(rayDir, rayDir);
	float b = 2.0 * dot(rayStart, rayDir);
	float c = dot(rayStart, rayStart) - (sphereRadius * sphereRadius);
	float d = b * b - 4.0 * a * c;
	if (d < 0.0)
	{
		return vec2(-1.0);
	}
	d = sqrt(d);
	return vec2(-b - d, -b + d) / (2.0 * a);
}
vec2 PlanetIntersection(vec3 rayStart, vec3 rayDir)
{
	return SphereIntersection(rayStart, rayDir, PLANET_CENTER, PLANET_RADIUS);
}
vec2 AtmosphereIntersection(vec3 rayStart, vec3 rayDir)
{
	return SphereIntersection(rayStart, rayDir, PLANET_CENTER, PLANET_RADIUS + ATMOSPHERE_HEIGHT);
}

// -------------------------------------
// Phase functions
float PhaseRayleigh(float costh)
{
	return 3.0 * (1.0 + costh*costh) / (16.0 * PI);
}
float PhaseMie(float costh, float g )// = 0.85)
{
	g = min(g, 0.9381);
	float k = 1.55*g - 0.55*g*g*g;
	float kcosth = k*costh;
	return (1.0 - k*k) / ((4.0 * PI) * (1.0-kcosth) * (1.0-kcosth));
}

// -------------------------------------
// Atmosphere
float AtmosphereHeight(vec3 positionWS)
{
	return distance(positionWS, PLANET_CENTER) - PLANET_RADIUS;
}
float DensityRayleigh(float h)
{
	return exp(-max(0.0, h / RAYLEIGH_HEIGHT));
}
float DensityMie(float h)
{
	return exp(-max(0.0, h / MIE_HEIGHT));
}
float DensityOzone(float h)
{
	// The ozone layer is represented as a tent function with a width of 30km, centered around an altitude of 25km.
	return max(0.0, 1.0 - abs(h - 25000.0) / 15000.0);
}
vec3 AtmosphereDensity(float h)
{
	return vec3(DensityRayleigh(h), DensityMie(h), DensityOzone(h));
}

// Optical depth is a unitless measurement of the amount of absorption of a participating medium (such as the atmosphere).
// This function calculates just that for our three atmospheric elements:
// R: Rayleigh
// G: Mie
// B: Ozone
// If you find the term "optical depth" confusing, you can think of it as "how much density was found along the ray in total".
vec3 IntegrateOpticalDepth( vec3 rayStart, vec3 rayDir)
{
	vec2 intersection = AtmosphereIntersection(rayStart, rayDir);
	float  rayLength    = intersection.y;

	const int sampleCount  = 8;
	float  stepSize = rayLength / float(sampleCount);
	
	vec3 opticalDepth = vec3(0.0);

	for (int i = 0; i < sampleCount; i++)
	{
		vec3 localPosition = rayStart + rayDir * (float(i) + 0.5) * stepSize;
		float  localHeight   = AtmosphereHeight(localPosition);
		vec3 localDensity  = AtmosphereDensity(localHeight);

		opticalDepth += localDensity * stepSize;
	}

	return opticalDepth;
}

// Calculate a luminance transmittance value from optical depth.
vec3 Absorb(vec3 opticalDepth)
{
	// Note that Mie results in slightly more light absorption than scattering, about 10%
	vec3 v = (opticalDepth.x * C_RAYLEIGH + opticalDepth.y * C_MIE * 1.1 + opticalDepth.z * C_OZONE) * ATMOSPHERE_DENSITY;
	return exp(-v);
}

// Integrate scattering over a ray for a single directional light source.
// Also return the transmittance for the same ray as we are already calculating the optical depth anyway.
vec3 IntegrateScattering(vec3 rayStart, vec3 rayDir, float rayLength, vec3 lightDir, vec3 lightColor, out vec3 transmittance)
{
	// We can reduce the number of atmospheric samples required to converge by spacing them exponentially closer to the camera.
	// This breaks space view however, so let's compensate for that with an exponent that "fades" to 1 as we leave the atmosphere.
	float  rayHeight = AtmosphereHeight(rayStart);
	float  sampleDistributionExponent = 1.0 + clamp(1.0 - rayHeight / ATMOSPHERE_HEIGHT,0.0,1.0) * 8.0; // Slightly arbitrary max exponent of 9

	vec2 intersection = AtmosphereIntersection(rayStart, rayDir);
	rayLength = min(rayLength, intersection.y);
	if (intersection.x > 0.0)
	{
		// Advance ray to the atmosphere entry point
		rayStart += rayDir * intersection.x;
		rayLength -= intersection.x;
	}

	float  costh    = dot(rayDir, lightDir);
	float  phaseR   = PhaseRayleigh(costh);
	float  phaseM   = PhaseMie(costh, 0.85);

	const int sampleCount  = 64;

	vec3 opticalDepth = vec3(0.0);
	vec3 rayleigh     = vec3(0.0);
	vec3 mie          = vec3(0.0);

	float  prevRayTime  = 0.0;

	for (int i = 0; i < sampleCount; i++)
	{
		float  rayTime = pow( float(i) / float(sampleCount), sampleDistributionExponent) * rayLength;
		// Because we are distributing the samples exponentially, we have to calculate the step size per sample.
		float  stepSize = (rayTime - prevRayTime);

		vec3 localPosition = rayStart + rayDir * rayTime;
		float  localHeight   = AtmosphereHeight(localPosition);
		vec3 localDensity  = AtmosphereDensity(localHeight);

		opticalDepth += localDensity * stepSize;

		// The atmospheric transmittance from rayStart to localPosition
		vec3 viewTransmittance = Absorb(opticalDepth);

		vec3 opticalDepthlight  = IntegrateOpticalDepth(localPosition, lightDir);
		// The atmospheric transmittance of light reaching localPosition
		vec3 lightTransmittance = Absorb(opticalDepthlight);

		rayleigh += viewTransmittance * lightTransmittance * phaseR * localDensity.x * stepSize;
		mie      += viewTransmittance * lightTransmittance * phaseM * localDensity.y * stepSize;

		prevRayTime = rayTime;
	}

	transmittance = Absorb(opticalDepth);

	return (rayleigh * C_RAYLEIGH + mie * C_MIE) * lightColor * EXPOSURE;
}
`;

var skybox_shader = IntegrateScattering + `
#define FLT_MAX 3.402823466e+38

varying vec3 v_position;
uniform vec3 u_camera_position;
uniform vec3 u_light_dir;
uniform vec3 u_light_color;

vec3 aces(vec3 x) {
  const float a = 2.51;
  const float b = 0.03;
  const float c = 2.43;
  const float d = 0.59;
  const float e = 0.14;
  return clamp((x * (a * x + b)) / (x * (c * x + d) + e), 0.0, 1.0);
}

void main()
{
	vec3 E = normalize( v_position - u_camera_position );

	float rayLength = FLT_MAX;
	vec3 transmittance;
	vec3 color = IntegrateScattering (u_camera_position, E, rayLength, u_light_dir, u_light_color, transmittance);
	color = aces(color);
	gl_FragColor = vec4(color, 1.0 );
}
`;

var illumination_shader = IntegrateScattering + `
#define FLT_MAX 3.402823466e+38

varying vec3 v_position;
varying vec3 v_normal;
uniform vec3 u_camera_position;
uniform vec3 u_light_dir;
uniform vec3 u_light_color;

vec3 aces(vec3 x) {
  const float a = 2.51;
  const float b = 0.03;
  const float c = 2.43;
  const float d = 0.59;
  const float e = 0.14;
  return clamp((x * (a * x + b)) / (x * (c * x + d) + e), 0.0, 1.0);
}

void main()
{
	vec3 N = normalize(v_normal);
	vec3 E = v_position - u_camera_position;
	float dist = length(E);
	E /= dist;

	float rayLength = dist;
	vec3 foo;
	vec3 lightTransmittance = Absorb(IntegrateOpticalDepth(v_position, u_light_dir));
	vec3 ambient = IntegrateScattering(v_position, vec3(0.0, 1.0, 0.0), FLT_MAX, u_light_dir, u_light_color, foo);

	float NdotL = dot(N, u_light_dir); // * 0.5 + 0.5; //testing half phong
	vec3 color = max(0.0, NdotL) * (ambient + u_light_color * lightTransmittance);

	vec3 transmittance;
	vec3 scattering = IntegrateScattering( u_camera_position, -E, rayLength, u_light_dir, u_light_color, transmittance);
	color = color * transmittance + scattering;

	color = aces(color); //tonemapper
	color = pow( color, vec3(2.2) ); //gamma
	gl_FragColor = vec4(color, 1.0 );
}
`;

	
	function init()
	{
		//create a scene
		var scene = new RD.Scene();

		//create the rendering context
		var context = GL.create({width: window.innerWidth, height:window.innerHeight});
		var renderer = new RD.Renderer( context );
		document.body.appendChild(renderer.canvas); //attach

		var shader = new GL.Shader( GL.Shader.DEFAULT_VERTEX_SHADER, skybox_shader );
		gl.shaders["sky"] = shader;

		var shader2 = new GL.Shader( GL.Shader.DEFAULT_VERTEX_SHADER, illumination_shader );
		gl.shaders["skylight"] = shader2;
		

		//create camera
		var camera = new RD.Camera();
		camera.perspective( 45, gl.canvas.width / gl.canvas.height, 1, 1000 );
		camera.lookAt( [3,0,-3],[0,0,0],[0,1,0] );

		//create a node
		var sky = new RD.SceneNode({
			color: [1,0,0,1],
			scale: 10,
			mesh: "sphere",
			shader: "sky"
		});
		sky.flags.two_sided = true;
		scene.root.addChild(sky);

		//create a node
		var ball = new RD.SceneNode({
			color: [1,0,0,1],
			scale: 1,
			mesh: "sphere",
			shader: "skylight"
		});
		scene.root.addChild(ball);


		//global settings
		var bg_color = vec4.fromValues(0.2,0.3,0.4,1);
		var light_dir = vec3.fromValues(0,1,0);
		var light_color = vec3.fromValues(1,1,1);

		renderer._uniforms.u_light_dir = light_dir;
		renderer._uniforms.u_light_color = light_color;

		//main render loop
		var last = now = getTime();
		requestAnimationFrame(animate);
		function animate() {
			requestAnimationFrame( animate );

			last = now;
			now = getTime();
			var dt = (now - last) * 0.001;

			vec3.rotateX( light_dir, light_dir, 0.002 );
			if(light_dir[1] < -0.3 && light_dir[2] > 0)
				light_dir[2] *= -1.0;

			vec3.normalize( light_dir, light_dir );

			renderer.clear(bg_color);
			renderer.render(scene, camera);
			scene.update(dt);
		}

		//input
		renderer.context.captureMouse();
		renderer.context.onmousemove = function(e)
		{
			if(e.dragging)
			{
				camera.orbit( e.deltax * 0.01, RD.UP );
				camera.orbit( e.deltay * 0.01, RD.RIGHT, null, true );
			}

		}
	}


	</script>
</head>
<body>
<script>init();</script>
</body>
</html>




